buildscript {
    repositories {
        maven {
            url 'https://oss.jfrog.org/artifactory/oss-snapshot-local'
        }
    }
    dependencies {
        classpath([group: 'org.m2ci.msp', name: 'jtgt', version: '0.6:SNAPSHOT'])
    }
}

plugins {
    id 'groovy'
    id 'de.undercouch.download' version '3.2.0'
    id "de.dfki.mary.voicebuilding.marytts-kaldi-mfa" version "0.1.0"
}

import de.undercouch.gradle.tasks.download.Download


task downloadData(type: Download) {
    src 'https://github.com/insa-k/marytts-voicebuilding-hvoice-data/releases/download/data/data.zip'
    dest "$buildDir/data.zip"
    overwrite false
    onlyIfNewer true
}

task unpackWav(type: Copy) {
    from zipTree("$buildDir/data.zip"), {
        include '**/wav/*.wav'
        eachFile {
            it.path = it.name
        }
    }
    into "$buildDir/wav"
    includeEmptyDirs = false
    dependsOn downloadData
}

task unpackText(type: Copy) {
    from zipTree("$buildDir/data.zip"), {
        include '**/text/*.txt'
        eachFile {
            it.path = it.name
        }
    }
    into "$buildDir/text"
    includeEmptyDirs = false
    dependsOn downloadData
}

task getData {
    dependsOn unpackText, unpackWav
}

convertTextToMaryXml {
    dependsOn unpackText, unpackWav
    srcDir = file("$buildDir/text")
}

prepareForcedAlignment {
    wavDir =  file("$buildDir/wav")
}
